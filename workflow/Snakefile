##################################################
# MODULES
import os
import re
import pandas

##################################################
# CONFIG

# Validation
# from snakemake.utils import validate
# validate(config, srcdir("../../schemas/config.schema.yaml"))

# Paths
SRC_DIR = srcdir("scripts")
ENV_DIR = srcdir("envs")
MOD_DIR = srcdir("../submodules")
DBS_DIR = config["dbsdir"]

# default executable for snakmake
shell.executable("bash")

# read in sample table and save in config object
config["samples_meta"] = os.path.abspath(config["samples"]) # absolute path to sample metadata
config["samples"] = pandas.read_csv(config["samples"], header=0, index_col=0, sep="\t")[["R1","R2"]].to_dict(orient="index")

# working directory
workdir:
    config["workdir"]

SAMPLES = config["samples"].keys()

##################################################
# RULES

include:
    "rules/fastq.smk"

include:
    "rules/asv.smk"

# "Master" rule
rule all:
    input:
        # FASTQ
        expand(
            "multiqc/fastqc/{ftype}/multiqc_report.html",
            ftype=["raw", "cutadapt_primers", "cutadapt_adapters"]
        ),
        expand(
            "multiqc/fastq/{ftype}/multiqc_report.html",
            ftype=["cutadapt_primers", "cutadapt_adapters"]
        ),
        # ASVs
        [
            "dada2/errormod.pdf",
            "dada2/ASV.counts.tsv", "dada2/ASV.fasta", "dada2/ASV.tax.tsv", "dada2/ASV.stats.tsv",
            "dada2/stats.pdf",
            "dada2/ASV.krona.html",
            "dada2/ASV.phyloseq.pdf"
        ]
